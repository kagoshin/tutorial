// 共通の設定
subprojects {
    apply plugin: 'java'
  
    def defaultEncoding = 'UTF-8'
    compileJava.options.encoding = defaultEncoding
  
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
  
    javadoc {
      options.links << 'http://docs.oracle.com/javase/jp/7/api/'
    }

    task commonTask << {
      println project.name + 'の' + name + 'タスクです。' 
    }
}
  
  
// ライブラリプロジェクト
project('arithmetic-lib') {
    task limitedTask << {
      println project.name + '専用のタスクです。'
    }
}
  
  
// アプリケーションプロジェクト
project('arithmetic-main') {
    apply plugin: 'application'
  
    mainClassName = 'com.example.cli.SimpleCalc'
    applicationName = 'SimpleCalc'
  
    dependencies {
      compile project(':arithmetic-lib')
    }
  
    run {
      standardInput = System.in
    }

    commonTask {
        doFirst {
            println '=' * 20 + " ${name} start " + '=' * 20
        }
        doLast {
            println '=' * 20 + " ${name}  end  " + '=' * 20
        }
        dependsOn ':arithmetic-lib:limitedTask'
    }
}