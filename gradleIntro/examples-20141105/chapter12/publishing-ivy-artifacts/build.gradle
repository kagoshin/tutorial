apply plugin: "java"
apply plugin: "ivy-publish" // Ivy Publishプラグインを適用

//...

group = "com.example"
version = 0.1

repositories {
  mavenCentral()
}

dependencies {
  runtime "org.apache.commons:commons-lang3:3.3.1"
}

task sourceJar(type: Jar) {
  classifier 'sources'
  from sourceSets.main.allJava // ソースセットを入力ファイルに設定
}
task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier 'javadoc'
  from javadoc.destinationDir
}

publishing {
  publications {
    mod1(IvyPublication) { // <mod1>は任意のモジュール識別名
      from components.java // ソフトウェアコンポーネント「java」を公開対象に指定する
      // artifact()メソッドで公開するアーカイブやファイルを指定する
      artifact(sourceJar) { // タスク名を指定すると、タスクによる出力ファイルが公開される
        type 'source'
      }
      artifact(javadocJar) {
        type 'doc'
      }
    }
  }
}

